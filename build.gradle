plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.google.cloud.tools.jib' version '3.4.2'
}

// jar 파일 생성 방지
bootJar.enabled = false

allprojects {
    group = 'com.attica'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }
}

sourceCompatibility = '17'

// 하위 모듈에 반영되는 부분
subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.cloud.tools.jib'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'

        annotationProcessor 'org.projectlombok:lombok'

        compileOnly 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':athens-api') {
    if (project.hasProperty('prod')) {
        jib {
            from {
                image = "eclipse-temurin:17-jdk"
            }
            to {
                image = System.getProperty('jib.to.image')
                tags = ["latest"]
            }
            container {
                mainClass = 'com.attica.athens.AthensApiApplication'
                creationTime = "USE_CURRENT_TIMESTAMP"

                jvmFlags = ["-Dspring.profiles.active=local", "-Duser.timezone=Asia/Seoul"]

                setAllowInsecureRegistries(true)
            }
        }
    }

    dependencies {
        implementation project(path: ':athens-support', configuration: 'default')
    }
}